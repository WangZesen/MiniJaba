/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.4/userguide/java_library_plugin.html
 */

apply plugin: 'java'
apply plugin: 'kotlin'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "org.yaml:snakeyaml:1.19"
    compile 'org.ow2.asm:asm:6.0'


    //compile 'org.bytedeco.javacpp-presets:llvm-platform:5.0.0-1.3.4-SNAPSHOT'
    compile 'org.bytedeco.javacpp-presets:llvm-platform:3.9.0-1.3'

    testCompile 'junit:junit:4.12'
}

buildscript {
    ext.kotlin_version = '1.2.10'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task jflex(type:Exec) {
    workingDir 'src/main/java/moe/taiho/minijaba'
    inputs.file file("${projectDir}/src/main/java/moe/taiho/minijaba/Lexer.l")
    outputs.files files("${projectDir}/src/main/java/moe/taiho/minijaba/Lexer.java")
    executable = 'jflex'
    args = [
            '-v',
            'Lexer.l',
    ]
}

task bison(type:Exec) {
    workingDir 'src/main/java/moe/taiho/minijaba'
    inputs.file file("${projectDir}/src/main/java/moe/taiho/minijaba/Parser.y")
    outputs.files files("${projectDir}/src/main/java/moe/taiho/minijaba/Parser.java",
            "${projectDir}/src/main/java/moe/taiho/minijaba/Parser.output")
    executable = 'bison'
    args = [
           '--verbose',
           '--warnings=all',
           'Parser.y',
    ]
}

compileJava.dependsOn jflex, bison